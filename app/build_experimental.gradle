apply plugin: 'com.android.model.application'

def ndk_skipX86 = (rootProject.hasProperty("readium_ndk_skipX86") && rootProject.readium_ndk_skipX86)
def ndk_skipARM = (rootProject.hasProperty("readium_ndk_skipARM") && rootProject.readium_ndk_skipARM)
def ndk_clang = (rootProject.hasProperty("readium_ndk_clang") && rootProject.readium_ndk_clang)
def ndk_experimental = (rootProject.hasProperty("readium_ndk_experimental") && rootProject.readium_ndk_experimental)

def key_password =  (rootProject.hasProperty("readium_sign_key_password") ? rootProject.readium_sign_key_password : "nope")
def store_password =  (rootProject.hasProperty("readium_sign_store_password") ? rootProject.readium_sign_store_password : "nope")

model {

    android.lintOptions {
        checkReleaseBuilds = false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError = false
    }

    // May require -Dorg.gradle.model.dsl=true
    // For example:
    //sh ./gradlew -Dorg.gradle.model.dsl=true assembleRelease
    android.signingConfigs {
        //mySigningConfig {
        create("mySigningConfig") {
            keyAlias = "ReadiumAndroidAPKSign"
            keyPassword = key_password
            storePassword = store_password
            storeType "jks"
            storeFile = new File("app/ReadiumAndroidAPKSign.jks")
        }
    }

    android {
        compileSdkVersion = 26
        buildToolsVersion = "27.0.3"

        defaultConfig {

            // manifest@package attribute in src/main/AndroidManifest.xml
            applicationId = "org.readium.sdk.android.launcher"

            // manifest/uses-sdk@android:minSdkVersion attribute in src/main/AndroidManifest.xml
            minSdkVersion.apiLevel = 19

            // manifest/uses-sdk@android:targetSdkVersion attribute in src/main/AndroidManifest.xml
            targetSdkVersion.apiLevel = 26

            // manifest@android:versionCode attribute in src/main/AndroidManifest.xml
            versionCode 31

            // manifest@android:versionName attribute in src/main/AndroidManifest.xml
            versionName "1.31"
        } //defaultConfig

        sources {
            main {
                java {
                    source {
                        srcDirs = [
                                './src/main/java',
                                '../../readium-sdk/Platform/Android/lib/src/main/java'
                        ]
                    } //source
                } //java

                jniLibs {
                    dependencies {
                        project ":epub3"
                    } //dependencies
                } //jniLibs
            } //main
        } //sources

        buildTypes {
            debug {
                minifyEnabled = false
            }
            release {
                minifyEnabled = false
                //proguardFiles.add(file('proguard-rules.txt'))

                //signingConfig = signingConfigs.mySigningConfig
                //signingConfig = $.android.signingConfigs.get("mySigningConfig")
                signingConfig = $("android.signingConfigs.mySigningConfig")
            }
        } //buildTypes

        productFlavors {
            if (!ndk_skipARM)
                create("arm") {
                    ndk.with {
                        abiFilters.add("armeabi-v7a")
                    }
                }

            if (!ndk_skipX86)
                create("x86") {
                    ndk.with {
                        abiFilters.add("x86")
                    }
                }
        } //productFlavors
    } //android
} //model